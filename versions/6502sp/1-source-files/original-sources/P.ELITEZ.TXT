   20REM    ELITE <Z>    - I/O Processor code
   25 
   26 
   40DIMW%&2400:REM Second Processor Only
  100C%=&2400:L%=C%:Z=0
  105ZP=&80:XX15=&90:X1=XX15:Y1=XX15+1:X2=XX15+2:Y2=XX15+3:SC=XX15+6:SCH=SC+1:OSTP=SC:P=FNZ:Q=FNZ:R=FNZ:S=FNZ:T=FNZ:SWAP=FNZ:T1=FNZ:COL=FNZ
  110OSSC=FNZT(2)
  150FF=&FF:OSWRCH=&FFEE:OSBYTE=&FFF4:OSWORD=&FFF1:OSFILE=&FFDD:SCLI=&FFF7:VIA=&FE40:USVIA=VIA:IRQ1V=&204:VSCAN=57:XX21=D%:WRCHV=&20E:WORDV=&20C:RDCHV=&210:NVOSWRCH=&FFCB
  155Tina=&B00
  160Y=96
  165protlen=0
  170PARMAX=15:REMparameters expected by RDPARAMS
  175RED=&F0
  180WHITE=&FA:WHITE2=&3F:RED2=&3:YELLOW2=&F:MAGNETA2=&33:CYAN2=&3C:BLUE2=&30:GREEN2=&C
  190STRIPE=&23
  200FORZ=4TO6STEP2
  220P%=C%:O%=W%:H%=L%+P%-C%
  222IF(P%AND255)<>0 PRINT"Must start on page boundary":STOP
  230FONT%=P%DIV256:IF Z=6 OSCLI("L.:2.C.FONT "+STR$~O%)
  240P%=P%+&300:O%=O%+&300
  300TABLE=&2300:REM Linestore buffer,one page
  700log=P%:logof2=LOG(2):IFZ=6 FORI%=1TO255:B%=&2000*LOG(I%)/logof2:B%=INT(B%+.5):I%?O%=B%DIV256:?(O%+I%+&100)=B%MOD256:NEXT
  710P%=P%+&200:O%=O%+&200:logL=log+&100
  720antilog=P%:IFZ=6 FORI%=0TO255:B%=INT(2^((I%/2+128)/16)+.5)DIV256:B%=B%+(B%=256):I%?O%=B%:NEXT
  730P%=P%+&100:O%=O%+&100
  740antilogODD=P%:IFZ=6 FORI%=0TO255:B%=INT(2^((I%/2+128.25)/16)+.5)DIV256:B%=B%+(B%=256):I%?O%=B%:NEXT
  750P%=P%+&100:O%=O%+&100
  760ylookup=P%:IFZ=6 FORI%=0TO255:B%=&40+((I%DIV8)*2):I%?O%=B%:NEXT
  770P%=P%+&100:O%=O%+&100
 1005[OPTZ
 1010.TVT3 EQUD&17243400:EQUD&47576474:EQUD&8696A1B1:EQUD&C6D6E1F1 \View  YRC
 1011      EQUD&17243400:EQUD&47576474:EQUD&8696A0B0:EQUD&C6D6E0F0 \Trade YRW
 1012      EQUD&17243400:EQUD&47576474:EQUD&8090A1B1:EQUD&C0D0E1F1 \Title YWC
 1013      EQUD&17243400:EQUD&47576474:EQUD&8292A0B0:EQUD&C2D2E0F0 \Trade YMW
 1014 
 1015\.............. Variables - do not alter after PARAMS .................
 1016 
 1017.XC EQUB1:.YC EQUB1:.K3 brk:.U brk:.LINTAB brk:.LINMAX brk:.YSAV brk:.svn brk:.PARANO brk:.DL brk:.VEC EQUW0:.HFX brk:.CATF brk:.K EQUD0
 1018.PARAMS:.ENERGY brk:.ALP1 brk:.ALP2 brk:.BETA brk:.BET1 brk:.DELTA brk:.ALTIT brk:.MCNT brk:.FSH brk:.ASH brk:.QQ14 brk:.GNTMP brk:.CABTMP brk:.FLH brk:.ESCP brk
 1020 
 1022\ Vectors for OSWRCH - routine should end with JMPPUTBACK once it has had its fill of data
 1023 
 1025.JMPTAB \Vector lookup table :EQUWUSOSWRCH:EQUWBEGINLIN:EQUWADDBYT:EQUWDOFE21 \3:EQUWDOHFX:EQUWSETXC \5:EQUWSETYC \6:EQUWCLYNS \7:EQUWRDPARAMS \8-GAME PARAMETERS:EQUWADPARAMS \9:EQUWDODIALS\10:EQUWDOVIAE\11:EQUWDOBULB\12:EQUWDOCATF\13
 1026EQUWDOCOL \14:EQUWSETVDU19 \15:EQUWDOSVN \16:EQUWDOBRK \17:EQUWprinter:EQUWprilf
 1027 
 1028\  ................ Called on loading only .....................
 1029 
 1030.STARTUP
 1032LDARDCHV:STAnewosrdch+1:LDARDCHV+1:STAnewosrdch+2:LDA#(newosrdch MOD256):SEI:STARDCHV:LDA#(newosrdch DIV256):STARDCHV+1 \~~
 1034LDA#&39:STAVIA+&E:LDA#&7F:STA&FE6E:LDAIRQ1V:STAVEC:LDAIRQ1V+1:STAVEC+1:LDA#IRQ1 MOD256:STAIRQ1V:LDA#IRQ1 DIV256:STAIRQ1V+1:LDA#VSCAN:STAUSVIA+5:CLI
 1035.NOINT LDAWORDV:STAnotours+1:LDAWORDV+1:STAnotours+2:LDA#NWOSWD MOD256:SEI:STAWORDV:LDA#NWOSWD DIV256:STAWORDV+1:CLI
 1036LDA#FF:STACOL:LDATina:CMP#ASC("T"):BNEPUTBACK:LDATina+1:CMP#ASC("I"):BNEPUTBACK:LDATina+2:CMP#ASC("N"):BNEPUTBACK:LDATina+3:CMP#ASC("A"):BNEPUTBACK:JSRTina+4
 1037 
 1038\ ............. OSWRCH revectored bumbling .....................
 1039 
 1040.PUTBACK LDA#128
 1050.USOSWRCH STXSC:TAX:BPLOHMYGOD:ASLA:TAX:CPX#39:BCSOHMYGOD:LDAJMPTAB,X:SEI:STAWRCHV:LDAJMPTAB+1,X:STAWRCHV+1:CLI:RTS:.OHMYGOD LDXSC:JMPTT26
 1060 
 1070.DODIALS TAX:LDA#6:SEI:STA&FE00:STX&FE01:CLI:JMPPUTBACK\hide dials on death:.DOFE21 STA&FE21:JMPPUTBACK \Shimmer on energy bomb
 1080.DOHFX STAHFX:JMPPUTBACK \Hyperspace colours
 1090.DOVIAE STAVIA+&E:JMPPUTBACK \Keyboard interrupt
 1095.DOCATF STACATF:JMPPUTBACK
 1097.DOCOL STACOL:JMPPUTBACK
 1098.DOSVN STAsvn:JMPPUTBACK
 1099.DOBRK BRK:EQUS"TTEST":EQUW13
 1100 
 1110.printer PHA:JSRTT26:PLA:CMP#11:BEQnottosend:PHA:LDA#2:JSRNVOSWRCH:PLA:PHA:CMP#32:BCStosend:CMP#10:BEQtosend2:LDA#13:JSRPOSWRCH:JMPsent:.tosend2
 1115\CMP#13\BEQsent:LDA#10:JSRPOSWRCH:.sent LDA#3:JSRNVOSWRCH:PLA:.nottosend JMPPUTBACK:.POSWRCH PHA:LDA#1:JSRNVOSWRCH:PLA:JMPNVOSWRCH:.tosend JSRPOSWRCH:JMPsent
 1117.prilf LDA#2:JSRNVOSWRCH:LDA#10:JSRPOSWRCH:JSRPOSWRCH:LDA#3:JSRNVOSWRCH:JMPPUTBACK
 1200.DOBULB TAX:BNEECBLB:LDA#16*8:STASC:LDA#&7B:STASC+1:LDY#15:.BULL LDASPBT,Y:EOR(SC),Y:STA(SC),Y:DEY:BPLBULL:JMPPUTBACK
 1210.ECBLB LDA#8*14:STASC:LDA#&7A:STASC+1:LDY#15:.BULL2 LDAECBT,Y:EOR(SC),Y:STA(SC),Y:DEY:BPLBULL2:JMPPUTBACK
 1250 
 1260.SPBT EQUD&FFAAFFFF:EQUD&FFFF00FF: EQUD&FF00FFFF:EQUD&FFFF55FF
 1270.ECBT EQUD&FFAAFFFF:EQUD&FFFFAAFF: EQUD&FF00FFFF:EQUD&FFFF00FF
 1290 
 1300.DOT LDY#2:LDA(OSSC),Y:STAX1:INY:LDA(OSSC),Y:STAY1:INY:LDA(OSSC),Y:STACOL:CMP#WHITE2:BNECPIX2:.CPIX4 JSRCPIX2:DECY1:.CPIX2 LDAY1
 1310\.CPIX:TAY:LDAylookup,Y:STASC+1:LDAX1:AND#&FC:ASLA:STASC:BCCP%+5:INCSC+1:CLC:TYA:AND#7:TAY:LDAX1:AND#2:TAX:LDACTWOS,X:ANDCOL:EOR(SC),Y:STA(SC),Y
 1320LDACTWOS+2,X:BPLCP1:LDASC:ADC#8:STASC:BCCP%+4:INCSC+1:LDACTWOS+2,X:.CP1 ANDCOL:EOR(SC),Y:STA(SC),Y:RTS
 1389 
 1390\  ...................... Scanners  ..............................
 1391 
 1400.SC48 LDY#4:LDA(OSSC),Y:STACOL:INY:LDA(OSSC),Y:STAX1:INY:LDA(OSSC),Y:STAY1:JSRCPIX4:LDACTWOS+2,X:ANDCOL:STAX1:STYQ:LDY#2:LDA(OSSC),Y:ASLA:INY:LDA(OSSC),Y:BEQRTS:LDYQ:TAX:BCCRTS+1
 1410.VLL1 DEY:BPLVL1:LDY#7:DECSC+1:DECSC+1:.VL1 LDAX1:EOR(SC),Y:STA(SC),Y:DEX:BNEVLL1:.RTS RTS
 1420INY:CPY#8:BNEVLL2:LDY#0:INCSC+1:INCSC+1
 1430.VLL2 INY:CPY#8:BNEVL2:LDY#0:INCSC+1:INCSC+1:.VL2 LDAX1:EOR(SC),Y:STA(SC),Y:INX:BNEVLL2:RTS
 1889 
 1890\.............Empty Linestore after copying over Tube .........
 1891 
 1900.BEGINLIN \was LL155 -CLEAR LINEstr
 1910STALINMAX:LDA#0:STALINTAB:LDA#&82:JMPUSOSWRCH:.RTS1 RTS
 1920.ADDBYT INCLINTAB:LDXLINTAB:STATABLE-1,X:INX:CPXLINMAX:BCCRTS1
 1950LDY#0:DECLINMAX:LDATABLE+3:CMP#FF:BEQdoalaser:.LL27 LDATABLE,Y:STAX1:LDATABLE+1,Y:STAY1:LDATABLE+2,Y:STAX2:LDATABLE+3,Y:STAY2:STYT1:JSRLOIN:LDAT1:CLC:ADC#4:.Ivedonealaser TAY:CMPLINMAX:BCCLL27:.DRLR1 JMPPUTBACK
 1960.doalaser LDACOL:PHA:LDA#RED:STACOL:LDATABLE+4:STAX1:LDATABLE+5:STAY1:LDATABLE+6:STAX2:LDATABLE+7:STAY2:JSRLOIN:PLA:STACOL:LDA#8:BNEIvedonealaser
 1989 
 1990\............. Line Draw ..............
 1994 
 1995.TWOS EQUD&11224488:.TWOS2 EQUD&333366CC:.CTWOS EQUD&5555AAAA:EQUW&AAAA
 1996.HLOIN2 LDXX1:STYY2:INY:STYQ:LDACOL:JMPHLOIN3 \any colour
 2000.LOIN
 2005LDA#128:STAS:ASLA:STASWAP:LDAX2:SBCX1:BCSLI1:EOR#FF:ADC#1:SEC:.LI1 STAP
 2010LDAY2:SBCY1:BEQHLOIN2:BCSLI2:EOR#FF:ADC#1:.LI2 STAQ:CMPP:BCCSTPX:JMPSTPY
 2015.STPX LDXX1:CPXX2:BCCLI3:DECSWAP:LDAX2:STAX1:STXX2:TAX:LDAY2:LDYY1:STAY1:STYY2:.LI3
 2020LDYY1:LDAylookup,Y:STASC+1:LDAY1:AND#7:TAY:TXA:AND#&FC:ASLA:STASC:BCCP%+4:INCSC+1
 2025TXA:AND#3:STAR
 2030LDXQ:BEQLIlog7:LDAlogL,X:LDXP:SEC:SBClogL,X:BMILIlog4:LDXQ:LDAlog,X:LDXP:SBClog,X:BCSLIlog5:TAX:LDAantilog,X:JMPLIlog6:.LIlog5 LDA#FF:BNELIlog6:.LIlog7 LDA#0:BEQLIlog6
 2031.LIlog4 LDXQ:LDAlog,X:LDXP:SBClog,X:BCSLIlog5:TAX:LDAantilogODD,X:.LIlog6 STAQ
 2035LDXP:BEQLIEXS:INX:LDAY2:CMPY1:BCCP%+5:JMPDOWN
 2040LDA#&88:ANDCOL:STALI100+1:LDA#&44:ANDCOL:STALI110+1:LDA#&22:ANDCOL:STALI120+1:LDA#&11:ANDCOL:STALI130+1
 2045LDASWAP:BEQLI190:LDAR:BEQLI100+6:CMP#2:BCCLI110+6:CLC:BEQLI120+6:BNELI130+6
 2050.LI190 DEX:LDAR:BEQLI100:CMP#2:BCCLI110:CLC:BEQLI120:JMPLI130
 2055.LI100 LDA#&88:EOR(SC),Y:STA(SC),Y:DEX:.LIEXS BEQLIEX:LDAS:ADCQ:STAS:BCCLI110:CLC:DEY:BMILI101
 2060.LI110 LDA#&44:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX:LDAS:ADCQ:STAS:BCCLI120:CLC:DEY:BMILI111
 2065.LI120 LDA#&22:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX:LDAS:ADCQ:STAS:BCCLI130:CLC:DEY:BMILI121
 2070.LI130 LDA#&11:EOR(SC),Y:STA(SC),Y:LDAS:ADCQ:STAS:BCCLI140:CLC:DEY:BMILI131:.LI140 DEX:BEQLIEX:LDASC:ADC#8:STASC:BCCLI100:INCSC+1:CLC:BCCLI100
 2075.LI101 DECSC+1:DECSC+1:LDY#7:BPLLI110
 2080.LI111 DECSC+1:DECSC+1:LDY#7:BPLLI120
 2085.LI121 DECSC+1:DECSC+1:LDY#7:BPLLI130
 2090.LI131 DECSC+1:DECSC+1:LDY#7:BPLLI140
 2095.LIEX RTS
 2100\.............
 2105.DOWN LDA#&88:ANDCOL:STALI200+1:LDA#&44:ANDCOL:STALI210+1:LDA#&22:ANDCOL:STALI220+1:LDA#&11:ANDCOL:STALI230+1:LDASC:SBC#&F8:STASC:LDASC+1:SBC#0:STASC+1:TYA:EOR#&F8:TAY
 2110LDASWAP:BEQLI191:LDAR:BEQLI200+6:CMP#2:BCCLI210+6:CLC:BEQLI220+6:BNELI230+6
 2115.LI191 DEX:LDAR:BEQLI200:CMP#2:BCCLI210:CLC:BEQLI220:BNELI230
 2120.LI200 LDA#&88:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX:LDAS:ADCQ:STAS:BCCLI210:CLC:INY:BEQLI201
 2125.LI210 LDA#&44:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX:LDAS:ADCQ:STAS:BCCLI220:CLC:INY:BEQLI211
 2130.LI220 LDA#&22:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX2:LDAS:ADCQ:STAS:BCCLI230:CLC:INY:BEQLI221
 2135.LI230 LDA#&11:EOR(SC),Y:STA(SC),Y:LDAS:ADCQ:STAS:BCCLI240:CLC:INY:BEQLI231:.LI240 DEX:BEQLIEX2:LDASC:ADC#8:STASC:BCCLI200:INCSC+1:CLC:BCCLI200
 2140.LI201 INCSC+1:INCSC+1:LDY#&F8:BNELI210
 2145.LI211 INCSC+1:INCSC+1:LDY#&F8:BNELI220
 2150.LI221 INCSC+1:INCSC+1:LDY#&F8:BNELI230
 2155.LI231 INCSC+1:INCSC+1:LDY#&F8:BNELI240
 2160.LIEX2 RTS
 2164 
 2165\.............
 2166 
 2170.STPY LDYY1:TYA:LDXX1:CPYY2:BCSLI15:DECSWAP:LDAX2:STAX1:STXX2:TAX:LDAY2:STAY1:STYY2:TAY
 2175.LI15 LDAylookup,Y:STASC+1:TXA:AND#&FC:ASLA:STASC:BCCP%+4:INCSC+1
 2180TXA:AND#3:TAX:LDATWOS,X:STAR
 2185LDXP:BEQLIfudge:LDAlogL,X:LDXQ:SEC:SBClogL,X:BMILIloG:LDXP:LDAlog,X:LDXQ:SBClog,X:BCSLIlog3:TAX:LDAantilog,X:JMPLIlog2:.LIlog3 LDA#FF:BNELIlog2:.LIloG LDXP:LDAlog,X:LDXQ:SBClog,X:BCSLIlog3:TAX:LDAantilogODD,X:.LIlog2 STAP:.LIfudge
 2188LDXQ:BEQLIEX7:INX:LDAX2:SEC:SBCX1:BCSP%+6:JMPLFT:.LIEX7 RTS
 2190LDASWAP:BEQLI290:TYA:AND#7:TAY:BNEP%+5:JMPLI307+8:CPY#2:BCSP%+5:JMPLI306+8:CLC:BNEP%+5:JMPLI305+8:CPY#4:BCSP%+5:JMPLI304+8:CLC:BNEP%+5:JMPLI303+8:CPY#6:BCSP%+5:JMPLI302+8:CLC:BEQP%+5:JMPLI300+8:JMPLI301+8
 2195.LI290 DEX:TYA:AND#7:TAY:BNEP%+5:JMPLI307:CPY#2:BCSP%+5:JMPLI306:CLC:BNEP%+5:JMPLI305:CPY#4:BCCLI304S:CLC:BEQLI303S:CPY#6:BCCLI302S:CLC:BEQLI301S:JMPLI300
 2200.LI310 LSRR:BCCLI301:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI301:INCSC+1:CLC:.LI301S BCCLI301
 2205.LI311 LSRR:BCCLI302:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI302:INCSC+1:CLC:.LI302S BCCLI302
 2210.LI312 LSRR:BCCLI303:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI303:INCSC+1:CLC:.LI303S BCCLI303
 2215.LI313 LSRR:BCCLI304:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI304:INCSC+1:CLC:.LI304S BCCLI304
 2220.LIEX3 RTS
 2225.LI300 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX3:DEY:LDAS:ADCP:STAS:BCSLI310
 2230.LI301 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX3:DEY:LDAS:ADCP:STAS:BCSLI311
 2235.LI302 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX3:DEY:LDAS:ADCP:STAS:BCSLI312
 2240.LI303 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX3:DEY:LDAS:ADCP:STAS:BCSLI313
 2245.LI304 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX4:DEY:LDAS:ADCP:STAS:BCSLI314
 2250.LI305 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX4:DEY:LDAS:ADCP:STAS:BCSLI315
 2255.LI306 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX4:DEY:LDAS:ADCP:STAS:BCSLI316
 2260.LI307 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX4:DECSC+1:DECSC+1:LDY#7:LDAS:ADCP:STAS:BCSP%+5:JMPLI300
 2265LSRR:BCSP%+5:JMPLI300:LDA#&88:STAR:LDASC:ADC#7:STASC:BCSP%+5:JMPLI300:INCSC+1:CLC:JMPLI300
 2270.LIEX4 RTS
 2275.LI314 LSRR:BCCLI305:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI305:INCSC+1:CLC:BCCLI305
 2280.LI315 LSRR:BCCLI306:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI306:INCSC+1:CLC:BCCLI306
 2285.LI316 LSRR:BCCLI307:LDA#&88:STAR:LDASC:ADC#7:STASC:BCCLI307:INCSC+1:CLC:BCCLI307
 2290\............
 2295.LFT LDASWAP:BEQLI291:TYA:AND#7:TAY:BNEP%+5:JMPLI407+8:CPY#2:BCSP%+5:JMPLI406+8:CLC:BNEP%+5:JMPLI405+8:CPY#4:BCSP%+5:JMPLI404+8:CLC:BNEP%+5:JMPLI403+8:CPY#6:BCSP%+5:JMPLI402+8:CLC:BEQP%+5:JMPLI400+8:JMPLI401+8
 2300.LI291 DEX:TYA:AND#7:TAY:BNEP%+5:JMPLI407:CPY#2:BCSP%+5:JMPLI406:CLC:BNEP%+5:JMPLI405:CPY#4:BCCLI404S:CLC:BEQLI403S:CPY#6:BCCLI402S:CLC:BEQLI401S:JMPLI400
 2305.LI410 ASLR:BCCLI401:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:.LI401S BCCLI401
 2310.LI411 ASLR:BCCLI402:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:.LI402S BCCLI402
 2315.LI412 ASLR:BCCLI403:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:.LI403S BCCLI403
 2320.LI413 ASLR:BCCLI404:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:.LI404S BCCLI404
 2325.LIEX5 RTS
 2330.LI400 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX5:DEY:LDAS:ADCP:STAS:BCSLI410
 2335.LI401 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX5:DEY:LDAS:ADCP:STAS:BCSLI411
 2340.LI402 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX5:DEY:LDAS:ADCP:STAS:BCSLI412
 2345.LI403 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX5:DEY:LDAS:ADCP:STAS:BCSLI413
 2350.LI404 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX6:DEY:LDAS:ADCP:STAS:BCSLI414
 2355.LI405 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX6:DEY:LDAS:ADCP:STAS:BCSLI415
 2360.LI406 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX6:DEY:LDAS:ADCP:STAS:BCSLI416
 2365.LI407 LDAR:ANDCOL:EOR(SC),Y:STA(SC),Y:DEX:BEQLIEX6:DECSC+1:DECSC+1:LDY#7:LDAS:ADCP:STAS:BCSP%+5:JMPLI400
 2370ASLR:BCSP%+5:JMPLI400:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:JMPLI400
 2375.LIEX6 RTS
 2380.LI414 ASLR:BCCLI405:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:BCCLI405
 2385.LI415 ASLR:BCCLI406:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:BCCLI406
 2390.LI416 ASLR:BCCLI407:LDA#&11:STAR:LDASC:SBC#8:STASC:BCSP%+4:DECSC+1:CLC:JMPLI407
 2392 
 2393\ ............HLOIN..........
 2394 
 2400.HLOIN LDY#0:LDA(OSSC),Y:STAQ:INY:INY:.HLLO LDA(OSSC),Y:STAX1:TAX:INY:LDA(OSSC),Y:STAX2:INY:LDA(OSSC),Y:STAY1:STYY2:AND#3:TAY:LDAorange,Y:.HLOIN3 STAS
 2405CPXX2:BEQHL6:BCCHL5:LDAX2:STAX1:STXX2:TAX:.HL5 DECX2
 2410LDYY1:LDAylookup,Y:STASC+1:TYA:AND#7:STASC:TXA:AND#&FC:ASLA:TAY:BCCP%+4:INCSC+1
 2420.HL1 TXA:AND#&FC:STAT:LDAX2:AND#&FC:SEC:SBCT:BEQHL2:LSRA:LSRA:STAR
 2430LDAX1:AND#3:TAX:LDATWFR,X:ANDS:EOR(SC),Y:STA(SC),Y:TYA:ADC#8:TAY:BCSHL7:.HL8 LDXR:DEX:BEQHL3
 2440CLC:.HLL1 LDAS:EOR(SC),Y:STA(SC),Y:TYA:ADC#8:TAY:BCSHL9:.HL10 DEX:BNEHLL1
 2450.HL3 LDAX2:AND#3:TAX:LDATWFL,X:ANDS:EOR(SC),Y:STA(SC),Y:.HL6 LDYY2:INY:CPYQ:BEQP%+5:JMPHLLO:RTS
 2460.HL2 LDAX1:AND#3:TAX:LDATWFR,X:STAT:LDAX2:AND#3:TAX:LDATWFL,X:ANDT:ANDS:EOR(SC),Y:STA(SC),Y:LDYY2:INY:CPYQ:BEQP%+5:JMPHLLO:RTS
 2462.HL7 INCSC+1:CLC:JMPHL8
 2464.HL9 INCSC+1:CLC:JMPHL10
 2470.TWFL EQUD&FFEECC88:.TWFR EQUD&113377FF
 2480.orange EQUB&A5:EQUB&A5:EQUB&5A:EQUB&5A
 2580 
 2590\..........Pixel........
 2595 
 2600.PIXEL LDY#0:LDA(OSSC),Y:STAQ:INY:INY:.PXLO LDA(OSSC),Y:STAP:AND#7:BEQPX5:TAX:LDAPXCL,X:STAS:INY:LDA(OSSC),Y:TAX:INY:LDA(OSSC),Y
 2620STYT1:TAY:LDAylookup,Y:STASC+1:TXA:AND#&FC:ASLA:STASC:BCCP%+4:INCSC+1:TYA:AND#7:TAY:TXA:AND#3:TAX
 2630LDAP:BMIPX3:CMP#&50:BCCPX2:LDATWOS2,X:ANDS:EOR(SC),Y:STA(SC),Y:LDYT1:INY:CPYQ:BNEPXLO:RTS
 2640.PX2 LDATWOS2,X:ANDS:EOR(SC),Y:STA(SC),Y:DEY:BPLP%+4:LDY#1
 2650LDATWOS2,X:ANDS:EOR(SC),Y:STA(SC),Y:LDYT1:INY:CPYQ:BNEPXLO:RTS
 2660.PX3 LDATWOS,X:ANDS:EOR(SC),Y:STA(SC),Y:LDYT1:INY:CPYQ:BNEPXLO:RTS
 2664\.....
 2670.PX5 INY:LDA(OSSC),Y:TAX:INY:LDA(OSSC),Y:STYT1:TAY:LDAylookup,Y:STASC+1:TXA:AND#&FC:ASLA:STASC:BCCP%+4:INCSC+1:TYA:AND#7:TAY:TXA:AND#3:TAX
 2680LDAP:CMP#&50:BCSPX6:LDATWOS2,X:AND#WHITE:EOR(SC),Y:STA(SC),Y:DEY:BPLP%+4:LDY#1:.PX6 LDATWOS2,X:AND#WHITE:EOR(SC),Y:STA(SC),Y:LDYT1:INY:CPYQ:BEQP%+5:JMPPXLO:RTS
 2700.PXCL EQUBWHITE:EQUB&F:EQUB&F:EQUB&F0:EQUB&F0:EQUB&A5:EQUB&A5:EQUB&F
 2990\...................
 3600 
 5000.newosrdch JSR&FFFF:CMP#128:BCCP%+6:.badkey LDA#7:CLC:RTS:CMP#32:BCScoolkey:CMP#13:BEQcoolkey:CMP#21:BNEbadkey:.coolkey CLC:RTS
 6200.ADD\AX=AP+SR
 6210STAT1:AND#128:STAT:EORS:BMIMU8:LDAR:CLC:ADCP:TAX:LDAS:ADCT1:ORAT:RTS
 6220.MU8 LDAS:AND#127:STAU:LDAP:SEC:SBCR:TAX:LDAT1:AND#127:SBCU:BCSMU9:STAU:TXA:EOR#FF:ADC#1:TAX:LDA#0:SBCU:ORA#128:.MU9 EORT:RTS
 6240 
 6900\..........Bay View..........
 6910 
 7000.HANGER LDX#2
 7100.HAL1 STXT:LDA#130:STXQ:JSRDVID4:LDAP:CLC:ADC#Y:TAY:LDAylookup,Y:STASC+1:STAR:LDAP:AND#7:STASC
 7110LDY#0:JSRHAS2
 7120LDYR:INY:STYSC+1:LDA#&40:LDY#&F8:JSRHAS3
 7130LDY#2:LDA(OSSC),Y:TAY:BEQHA2:LDY#0:LDA#&88:JSRHAL3:DECSC+1:LDY#&F8:LDA#&10:JSRHAS3
 7140.HA2 LDXT:INX:CPX#13:BCCHAL1
 7150LDA#60:STAS:LDA#&10:LDX#&40:STXR
 7160.HAL6 LDXR:STXSC+1:STAT:AND#&FC:STASC:LDX#&88:LDY#1
 7170.HAL7 TXA:AND(SC),Y:BNEHA6:TXA:AND#RED:ORA(SC),Y:STA(SC),Y:INY:CPY#8:BNEHAL7:INCSC+1:INCSC+1:LDY#0:BEQHAL7
 7180.HA6 LDAT:CLC:ADC#16:BCCP%+4:INCR:DECS:BNEHAL6
 7190.HA3 RTS
 7200.HAS2 LDA#&22:.HAL2 TAX:AND(SC),Y:BNEHA3:TXA:AND#RED:ORA(SC),Y:STA(SC),Y:TXA:LSRA:BCCHAL2:TYA:ADC#7:TAY:LDA#&88:BCCHAL2
 7205INCSC+1:.HAL3 TAX:AND(SC),Y:BNEHA3:TXA:AND#RED:ORA(SC),Y:STA(SC),Y:TXA:LSRA:BCCHAL3:TYA:ADC#7:TAY:LDA#&88:BCCHAL3:RTS
 7210.HAS3 TAX:AND(SC),Y:BNEHA3:TXA:ORA(SC),Y:STA(SC),Y:TXA:ASLA:BCCHAS3:TYA:SBC#8:TAY:LDA#&10:BCSHAS3:RTS
 7300.DVID4 \P=A DIV Q
 7310LDX#8:ASLA:STAP:LDA#0:.DVL4 ROLA:BCSDV8:CMPQ:BCCDV5:.DV8 SBCQ:.DV5 ROLP:DEX:BNEDVL4:RTS
 7500 
 7501\ .............. Input game parameters (DIALS) .................
 7502 
 7510.ADPARAMS INCPARANO:LDXPARANO:STAPARAMS-1,X:CPX#PARMAX:BCSP%+3:RTS:JSRDIALS:JMPPUTBACK:.RDPARAMS LDA#0:STAPARANO
 7590LDA#&89:JMPUSOSWRCH
 7650 
 7651\ ................... Keyboard ................................
 7652 
 7660.KYTB EQUB0:EQUB&E8:EQUB&E2:EQUB&E6:EQUB&E7:EQUB&C2:EQUB&D1:EQUB&C1:EQUD&35237060:EQUW&2265:EQUB&45:EQUB&52 \? <>XSA.FBRLtabescTUMEJC
 7665NOP
 7700.KEYBOARD LDY#9:.DKL2 LDAKYTB-2,Y:EQUS FNDKS4:ASLA:LDA#0:ADC#FF:EOR#FF:STA(OSSC),Y:DEY:CPY#2:BNEDKL2 \-ve INKEY
 7710LDA#16:SED:.DKL3 EQUS FNDKS4:TAX:BMIDK1:CLC:ADC#1:BPLDKL3:.DK1 CLD:EOR#128:STA(OSSC),Y
 7720LDX#1:LDA#&80:JSROSBYTE:TYA:LDY#10:STA(OSSC),Y:LDX#2:LDA#&80:JSROSBYTE:TYA:LDY#11:STA(OSSC),Y:LDX#3:LDA#&80:JSROSBYTE:TYA:LDY#12:STA(OSSC),Y:LDY#14:LDA&FE40:STA(OSSC),Y:.DK2 RTS
 7739 
 7740\ ......... Revectoring of OSWORD ...............................
 7741 
 7780.OSWVECS EQUWKEYBOARD:EQUWPIXEL:EQUWMSBAR:EQUWWSCAN:EQUWSC48:EQUWDOT
 7781         EQUWDODKS4:EQUWHLOIN:EQUWHANGER
 7784EQUWSOMEPROT:EQUWSAFE:EQUWSAFE:EQUWSAFE:EQUWSAFE:EQUWSAFE:EQUWSAFE:EQUWSAFE:EQUWSAFE:EQUWSAFE
 7789 
 7790\ Above vector lookup table is JSRed below, after registers are preserved.
 7791\ OSSC points to the parameter block, and should not be corrupted.
 7792\ Copy into SC if it may be corrupted.  End with an RTS
 7793 
 7800.NWOSWD BITsvn:BMInotours:CMP#240:BCCnotours:STXOSSC:STYOSSC+1:PHA:SBC#240:ASLA:TAX:LDAOSWVECS,X:STAJSRV+1:LDAOSWVECS+1,X:STAJSRV+2:LDXOSSC
 7810.JSRV JSR&FFFC \Poked over:PLA:LDXOSSC:LDYOSSC+1:.SAFE RTS:.notours JMP&FFFC \~~
 7820 
 7850.MSBAR LDY#2:LDA(OSSC),Y:ASLA:ASLA:ASLA:ASLA:STAT:LDA#97:SBCT:STASC:LDA#&7C:STASCH:LDY#3:LDA(OSSC),Y:LDY#5:.MBL1 STA(SC),Y:DEY:BNEMBL1:PHA:LDASC:CLC:ADC#8:STASC:PLA:AND#&AA:LDY#5:.MBL2 STA(SC),Y:DEY:BNEMBL2:RTS
 7860.WSCAN LDA#0:STADL:.WSCAN1 LDADL:BEQWSCAN1:RTS
 7870.DODKS4 LDY#2:LDA(OSSC),Y:EQUS FNDKS4:STA(OSSC),Y:RTS
 8099 
 8100\ ............. Character Print .....................
 8101 
 8190.cls JSRTTX66:JMPRR4
 8194.TT67 LDA#12
 8200.TT26 \PRINT
 8210STAK3:TYA:PHA:TXA:PHA:LDAK3:TAY:BEQRR4S:CMP#11:BEQcls:CMP#7:BNEP%+5:JMPR5:CMP#32:BCSRR1:CMP#10:BEQRRX1:LDX#1:STXXC:.RRX1 CMP#13:BEQRR4S:INCYC:.RR4S JMPRR4
 8220.RR1 TAY:\BEQRR4:BPLP%+5:JMPRR4:LDX#(FONT%-1):ASLA:ASLA:BCCP%+4:LDX#(FONT%+1):ASLA:BCCP%+3:INX:STAQ:STXR
 8240LDAXC:LDXCATF:BEQRR5:CPY#32:BNERR5:CMP#17:BEQRR4:.RR5 ASLA:ASLA:ASLA:STASC:LDAYC:CPY#&7F:BNERR2:DECXC:ASLA:ASLSC:ADC#&3F:TAX:LDY#&F0:JSRZES2:BEQRR4
 8245.RR2 INCXC:CMP#24:BCCRR3:PHA:JSRTTX66:LDA#1:STAXC:STAYC:PLA:LDAK3:JMPRR4
 8250.RR3 ASLA:ASLSC:ADC#&40:.RREN STASC+1:LDASC:CLC:ADC#8:STAS:LDASC+1:STAT:LDY#7:.RRL1 LDA(Q),Y:AND#&F0:STAU:LSRA:LSRA:LSRA:LSRA:ORAU:ANDCOL:EOR(SC),Y:STA(SC),Y:LDA(Q),Y:AND#&F:STAU:ASLA:ASLA:ASLA:ASLA:ORAU:ANDCOL:EOR(S),Y:STA(S),Y:DEY
 8252BPLRRL1:.RR4 PLA:TAX:PLA:TAY:LDAK3:.rT9 RTS:.R5 LDX#(BELI MOD256):LDY#(BELI DIV256):JSROSWORD:JMPRR4
 8255.BELI EQUW &12:EQUW&FFF1:EQUW200:EQUW2
 8260\....
 8300.TTX66 LDX#&40:.BOL1 JSRZES1:INX:CPX#&70:BNEBOL1:.BOX LDA#&F:STACOL:LDY#1:STYYC:LDY#11:STYXC:LDX#0:STXX1:STXY1:STXY2:\STXQQ17:DEX:STXX2:JSRLOIN:LDA#2:STAX1:STAX2:JSRBOS2:.BOS2 JSRBOS1:.BOS1 LDA#0:STAY1:LDA#2*Y-1:STAY2:DECX1:DECX2
 8310JSRLOIN:LDA#&F:STA&4000:STA&41F8:RTS
 8350 
 8410.ZES1 LDY#0:STYSC:.ZES2 LDA#0:STXSC+1:.ZEL1 STA(SC),Y:INY:BNEZEL1:RTS
 8999 
 9000.SETXC STAXC:JMPPUTBACK:.SETYC STAYC:JMPPUTBACK
 9010 
 9300.SOMEPROT LDY#2:.SMEPRTL LDAdo65C02-2,Y:STA(OSSC),Y:INY:CPY#protlen+2:BCCSMEPRTL:RTS
 9400 
 9460.CLYNS LDA#20:STAYC:LDA#&6A:STASC+1:JSRTT67:LDA#0:STASC:LDX#3:.CLYL LDY#8:.EE2 STA(SC),Y:INY:BNEEE2:INCSC+1:STA(SC),Y:LDY#&F7:.EE3 STA(SC),Y:DEY:BNEEE3:INCSC+1:DEX:BNECLYL:\INX\STXSC:JMPPUTBACK
 9489 
 9490\ ............... DIALS ..........................
 9491 
 9501.DIALS LDA#1:STAVIA+&E:LDA#&A0:STASC:LDA#&71:STASC+1:JSRPZW2:STXK+1:STAK:LDA#14:STAT1:LDADELTA:\LSRA:JSRDIL-1
 9502LDA#0:STAR:STAP:LDA#8:STAS:LDAALP1:LSRA:LSRA:ORAALP2:EOR#128:JSRADD:JSRDIL2:LDABETA:LDXBET1:BEQP%+4:SBC#1:JSRADD:JSRDIL2
 9503\LDAMCNT:\AND#3:\BEQP%+3:\RTS
 9504LDY#0:JSRPZW:STXK:STAK+1:LDX#3:STXT1:.DLL23 STYXX15,X:DEX:BPLDLL23:LDX#3:LDAENERGY:LSRA:LSRA:STAQ:.DLL24 SEC:SBC#16:BCCDLL26:STAQ:LDA#16:STAXX15,X:LDAQ:DEX:BPLDLL24:BMIDLL9:.DLL26
 9505LDAQ:STAXX15,X:.DLL9 LDAXX15,Y:STYP:JSRDIL:LDYP:INY:CPY#4:BNEDLL9
 9506LDA#&70:STASC+1:LDA#&20:STASC:LDAFSH:JSRDILX:LDAASH:JSRDILX:LDA#YELLOW2:STAK:STAK+1:LDAQQ14:JSRDILX+2
 9510JSRPZW2:STXK+1:STAK:LDX#11:STXT1:LDACABTMP:JSRDILX:LDAGNTMP:JSRDILX
 9530LDA#&F0:STAT1:LDA#YELLOW2:STAK:STAK+1:LDAALTIT:JMPDILX
 9540.PZW2 LDX#WHITE2:EQUB&2C:.PZW LDX#STRIPE:LDAMCNT:AND#8:ANDFLH:BEQP%+5:LDA#GREEN2:RTS:LDA#RED2:RTS
 9690.DILX LSRA:LSRA:LSRA:LSRA
 9700.DIL STAQ:LDX#FF:STXR:CMPT1:BCSDL30:LDAK+1:BNEDL31:.DL30 LDAK:.DL31 STACOL:LDY#2:LDX#7:.DL1 LDAQ:CMP#2:BCCDL2:SBC#2:STAQ:LDAR:.DL5 ANDCOL:STA(SC),Y:INY:STA(SC),Y:INY:STA(SC),Y:TYA:CLC:ADC#6:TAY:DEX:BMIDL6:BPLDL1
 9710.DL2 EOR#1:STAQ:LDAR:.DL3 ASLA:AND#&AA:DECQ:BPLDL3:PHA:LDA#0:STAR:LDA#99:STAQ:PLA:JMPDL5:.DL6 INCSC+1:INCSC+1:.DL9 RTS
 9712.DIL2 LDY#1:STAQ:.DLL10 SEC:LDAQ:SBC#2:BCSDLL11:LDA#FF:LDXQ:STAQ:LDACTWOS,X:AND#WHITE2:BNEDLL12:.DLL11 STAQ:LDA#0:.DLL12 STA(SC),Y:INY:STA(SC),Y:INY:STA(SC),Y:INY:STA(SC),Y:TYA:CLC:ADC#5:TAY:CPY#60:BCCDLL10:INCSC+1:INCSC+1:RTS
 9718 
 9720\ .................... Interrupts .........................
 9721 
 9730.TVT1 EQUD&16254334:EQUD&52617086:EQUD&96A5B4C3:EQUD&D2E1F007 \Dials
 9735.do65C02:.whiz LDA(0):PHA:LDA(2):STA(0):PLA:STA(2):\NOP\NOP\NOP\NOP:INC0:BNEP%+4:INC1:LDA2:BNEP%+4:DEC3:DEC2\SC=2:DEA:CMP0:LDA3:SBC1:BCSwhiz:JMP(0,X):.end65C02 \**
 9736]protlen=end65C02-do65C02:[OPTZ
 9739.IRQ1 TYA:PHA:LDY#15:LDA#2:BITVIA+&D:BNELINSCN:BVCjvec:LDA#&14:STA&FE20:LDAESCP:AND#4:EOR#&34:STA&FE21\ESCP:.VNT2:LDATVT1,Y:STA&FE21:DEY:BNEVNT2:.jvec PLA:TAY:JMP(VEC)
 9740.LINSCN LDA#30:STADL:STAUSVIA+4:LDA#VSCAN:STAUSVIA+5:LDAHFX:BNEjvec:LDA#&18:STA&FE20:.VNT3 LDATVT3,Y:STA&FE21:DEY:BNEVNT3:\LDAsvn:\BMIjvec:PLA:TAY:LDA&FE41:LDA&FC:RTI
 9750.SETVDU19 STAVNT3+1:JMPPUTBACK
 9900]NEXTZ
 9905INPUT"Insert destination disk and hit RETURN"A$
 9910OSCLI("S.I.CODE "+STR$~W%+" "+STR$~O%+" FFFF"+STR$~STARTUP+" FFFF"+STR$~H%)
 9920PRINT"CODE:";~C%",";~P%" (";&4000-P%" Free)  ZP:";~ZP;"Prot: ";~protlen
 9930END
10000DEFFNZT(N%):ZP=ZP+N%:=ZP-N%
10010DEFFNZ:ZP=ZP+1:=ZP-1
20000DEFFNDKS4:[OPTZ:LDX#3:SEI:STX&FE40:LDX#&7F:STX&FE43:STA&FE4F:LDA&FE4F:LDX#&B:STX&FE40:CLI:]:=""

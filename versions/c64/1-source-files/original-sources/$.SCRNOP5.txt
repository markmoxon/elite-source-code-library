   10REM ScrnOpns1  ---- MODE 5  PJMI
   15REM Vastly improved by ICGB
   20REM SCREEN TOUCH UP PROGRAM
   30REM DONT SCROLL SCREEN
   40REM S-Show cell:f6/f7 STO/RCL cell
   50REM f8 SAVE:f2/4,f3/5,f4/6 plotting by SET,XOR,CLR for small/big points
   60REM COPY moves large areas. Move cursor to corner of rectangle, press COPY, move cursor to opposite corner, press COPY, move cursor to corner of destination.
   70REM RETURN does same but swaps
   80REM f0/f1 fast/slow cursor
   90REM DELETE clears rectangles - go top left then bottom right
   92REM 0,1,2,3 change colours
   95ON ERROR GOTO 810
  100MODE5:VDU19,3,2,0,0,0
  110 
  120VDU28,1,31,18,31 :REM Window ****
  130INPUTA$:OSCLI("L. "+A$+" 6F80")
  140*FX4 1
  150*FX12 2
  160*K.0|!A
  170*K.1|!B
  180*K.2|!C 
  190*K.3|!D
  200*K.4|!E
  210*K.5|!F
  220*K.6|!G
  230*K.7|!H
  240*K.8|!I
  250*K.9*FX4|MV.26|M*FX12,4|M
  260X%=128:Y%=128:WIN=0:COP=0:DEL=0
  270*FX15,1
  280C%=0:REPEAT
  290PLOT70,X%*4,Y%*4
  300C%=C%EOR1
  310I=INKEY(10):UNTILI<>-1
  320IFC%=1PLOT70,X%*4,Y%*4
  330IFI=137X%=ABS(X%+D%)MOD320:GOTO270
  340IFI=136X%=ABS(X%-D%)MOD320:GOTO270
  350IFI=139Y%=Y%+D%AND&FF:GOTO270
  360IFI=138Y%=Y%-D%AND&FF:GOTO270
  370IFI=13PROCWIND:GOTO270
  380IFI=135PROCCOPY:GOTO270
  385IFI=127PROCDEL:GOTO270
  390IFI=83PROCFLASH:GOTO270
  400IFI=65+128D%=2:GOTO270
  410IFI=66+128D%=1:GOTO270
  420 
  430IFI=67+128PROCP(0,X%,Y%):GOTO270
  440IFI=68+128PROCP(1,X%,Y%):GOTO270
  450IFI=69+128PROCP(2,X%,Y%):GOTO270
  452IFI=48GCOL0,0
  454IFI=49GCOL0,1
  456IFI=50GCOL0,2
  458IFI=51GCOL0,3
  460IFI=52PROCP(0,X%,Y%):PROCP(0,X%+1,Y%):PROCP(0,X%+1,Y%-1):PROCP(0,X%,Y%-1):GOTO270
  470IFI=53PROCP(1,X%,Y%):PROCP(1,X%+1,Y%):PROCP(1,X%+1,Y%-1):PROCP(1,X%,Y%-1):GOTO270
  480IFI=54PROCP(2,X%,Y%):PROCP(2,X%+1,Y%):PROCP(2,X%+1,Y%-1):PROCP(2,X%,Y%-1):GOTO270
  490IFI=71+128PROCSTO:GOTO270
  500IFI=72+128PROCRCL:GOTO270
  510IFI=73+128 PROCsve:GOTO270
  520GOTO270
  530DEFPROCaddr
  540A%=&5800+(X%AND&FF8)+((255-Y%)AND7)+((255-Y%)AND&F8)*40
  550ENDPROC
  560DEFPROCchar:A%=&5800+(X%AND&FF8)+((255-Y%)AND&F8)*40:ENDPROC
  570DEFPROCFLASH:PROCchar:FORJ%=0TO1:FORI%=0TO7:I%?A%=I%?A%EOR255:NEXT:FORK%=0TO100:NEXT:NEXT:ENDPROC
  580DEFPROCP(I%,J%,K%):PLOT69+I%,J%*4,K%*4:ENDPROC
  590DEFPROCSTO
  600INPUT"STO no.?"N:IFN>63ORN<0THEN600ELSEPROCchar
  610FORI%=0TO7:?(N*8+I%+&900)=I%?A%:NEXT:ENDPROC
  620DEFPROCRCL
  630INPUT"RCL no.?"N:PROCchar
  640FORI%=0TO7:I%?A%=?(N*8+I%+&900):NEXT:ENDPROC
  650DEFPROCWIND
  660WIN=WIN+1:PROCS(100)
  670IFWIN=1:WXL=X%:WYU=Y%:ENDPROC
  680IFWIN=2:WXR=X%:WYD=Y%:ENDPROC
  690WIN=0
  700FORI%=0TOWXR-WXL:L%=(X%+I%)*4:M%=(WXL+I%)*4:FORJ%=0TOWYU-WYD:K%=POINT(M%,(WYU-J%)*4):PLOT71-2*POINT(L%,(Y%-J%)*4),M%,(WYU-J%)*4:PLOT71-2*K%,L%,(Y%-J%)*4:NEXT:NEXT:PROCS(120):ENDPROC
  710DEFPROCCOPY
  720COP=COP+1:PROCS(1)
  730IFCOP=1:CXL=X%:CYU=Y%:ENDPROC
  740IFCOP=2:CXR=X%:CYD=Y%:ENDPROC
  750COP=0:FORI%=0TOCXR-CXL:L%=(X%+I%)*4:M%=(CXL+I%)*4:FORJ%=0TOCYU-CYD:PLOT71-2*POINT(M%,(CYU-J%)*4),L%,(Y%-J%)*4:NEXT:NEXT:PROCS(16):ENDPROC
  760DEFPROCsve:CLS:*OPT1,0
  770INPUTA$:CLS:OSCLI("S."+A$+" 6F80+8C8")
  800ENDPROC
  810DEFPROCS(E%):SOUND1,-7,E%,4:ENDPROC
  820IFERR<>17REPORT:PRINT" at line ";ERL;" , I'm afraid.":PROCS(90):PROCS(70):END
  830PRINT"Abort?":IFGET$<>"Y"GOTO270
  840*FX4
  850REMVDU26,7
  860END
  900DEFPROCDEL:DEL=DEL+1:PROCS(1)
  910IFDEL=1:DXL=X%:DYU=Y%:ENDPROC
  920DXR=X%:DYD=Y%
  930DEL=0:MOVE4*DXL,4*DYD:MOVE4*DXR,4*DYD:PLOT87,4*DXL,4*DYU:PLOT87,4*DXR,4*DYU:PROCS(16):ENDPROC

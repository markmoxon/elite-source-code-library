    5REM Source Code for Loader
   10C%=&F40:S%=C%:L%=&1128:D%=&563A:LC%=&6000-C%:svn=&7FFD
   20MOS=S%+8:TRTB%=4
  100DIM CODE &1000
 1000OSWRCH=&FFEE:OSBYTE=&FFF4:OSWORD=&FFF1:SCLI=&FFF7:IRQ1V=&204
 1010ZP=&70:P=&72:Q=&73:YY=&74:T=&75
 1020FF=&FF
 1030VIA=&FE40:USVIA=VIA:VSCAN=57:VEC=&7FFE
 3000FORZ=4TO6STEP2
 3010P%=&900:O%=CODE
 3015RESTORE:READN%
 3020B%=P%:FORI%=0TON%-1:READA:PROCP(A):NEXT
 3100E%=P%:FORI%=1TO4:FORJ%=1TO14:READA:PROCP(A):NEXT,
 4000[OPTZ
 5000.ENTRY LDA#16:LDX#3:JSROSBYTE \ADC
 5001LDA#190:LDX#8:JSROSB \8bitADC
 5002LDA#200:LDX#3:JSROSB \break,escape
 5004LDA#13:LDX#0:JSROSB \otput bffer
 5005LDA#144:LDX#255:JSROSB \TV
 5006LDA#225:LDX#128:JSROSB \fn keys
 5007LDA#13:LDX#2:JSROSB \kybrd buffer
 5008LDA#(B% MOD256):STAZP:LDA#(B% DIV256):STAZP+1:LDY#0:.LOOP LDA(ZP),Y:JSROSWRCH:INY:CPY#N%:BNELOOP \set up mode
 5010LDA#4:LDX#1:JSROSB \cursor
 5020LDA#9:LDX#0:JSROSB \flashing
 5030LDA#172:LDX#0:LDY#FF:JSROSBYTE:STXTRTB%:STYTRTB%+1 \int-ascii table
 5050JSRPLL1
 5100EQUS FNE(0):EQUS FNE(1):EQUS FNE(2):EQUS FNE(3) \envelopes
 5110LDX#(MESS2 MOD256):LDY#(MESS2 DIV256):JSRSCLI
 5120LDX#(MESS3 MOD256):LDY#(MESS3 DIV256):JSRSCLI
 5130LDX#(MESS4 MOD256):LDY#(MESS4 DIV256):JSRSCLI
 5140LDX#(MESS5 MOD256):LDY#(MESS5 DIV256):JSRSCLI
 5150LDX#(MESS6 MOD256):LDY#(MESS6 DIV256):JSRSCLI
 5160LDX#(MESS7 MOD256):LDY#(MESS7 DIV256):JSRSCLI
 5170LDX#(MESS8 MOD256):LDY#(MESS8 DIV256):JSRSCLI
 5180LDX#(MESS9 MOD256):LDY#(MESS9 DIV256):JSRSCLI
 5190LDX#(MESS10 MOD256):LDY#(MESS10 DIV256):JSRSCLI
 5200LDX#(MESS11 MOD256):LDY#(MESS11 DIV256):JSRSCLI
 5310LDA#140:LDX#12:JSROSBYTE
 5320LDA#0:STAsvn
 5350LDX#(LC% DIV256):LDA#(L% MOD256):STAZP:LDA#(L% DIV256):STAZP+1:LDA#(C% MOD256):STAP:LDA#(C% DIV256):STAP+1:LDY#0
 5360.ML1 LDA(ZP),Y:STA(P),Y:INY:BNEML1:INCZP+1:INCP+1:DEX:BPLML1
 5370LDAS%+6:STA&202:LDAS%+7:STA&203:LDAS%+2:STA&20E:LDAS%+3:STA&20F
 5380.SETUP SEI:LDAVIA+4:STA1:LDA#&39:STAVIA+&E:LDA#&7F:STA&FE6E:LDAIRQ1V:STAVEC:LDAIRQ1V+1:STAVEC+1:LDAS%+4:STAIRQ1V:LDAS%+5:STAIRQ1V+1:LDA#VSCAN:STAUSVIA+5:CLI
 5385LDA#&81:LDY#FF:LDX#1:JSROSBYTE:TXA:EOR#FF:STAMOS \FF if MOS0.1 else 0
 5390JMP(S%)
 5400.PLL1 LDAVIA+4:STARAND+1:JSRDORND:JSRSQUA2:STAZP+1:LDAP:STAZP:JSRDORND:STAYY:JSRSQUA2:TAX:LDAP:ADCZP:STAZP:TXA:ADCZP+1:BCSPLC1
 5410STAZP+1:LDA#1:SBCZP:STAZP:LDA#&40:SBCZP+1:STAZP+1:BCCPLC1:JSRROOT:LDAZP:LSRA:TAX:LDAYY:CMP#128:RORA:JSRPIX
 5420.PLC1 DECCNT:BNEPLL1:DECCNT+1:BNEPLL1
 5450.PLL2 JSRDORND:TAX:JSRSQUA2:STAZP+1:JSRDORND:STAYY:JSRSQUA2:ADCZP+1:CMP#&11:BCCPLC2:LDAYY:JSRPIX
 5460.PLC2 DECCNT2:BNEPLL2:DECCNT2+1:BNEPLL2
 5500.PLL3 JSRDORND:STAZP:JSRSQUA2:STAZP+1:JSRDORND:STAYY:JSRSQUA2:STAT
 5510ADCZP+1:STAZP+1:LDAZP:CMP#128:RORA:CMP#128:RORA:ADCYY:TAX:JSRSQUA2:TAY:ADCZP+1
 5520BCSPLC3:CMP#&50:BCSPLC3:CMP#&20:BCCPLC3:TYA:ADCT:CMP#&10:BCSPL1:LDAZP:BPLPLC3:.PL1 LDAYY:JSRPIX
 5550.PLC3 DECCNT3:BNEPLL3:DECCNT3+1:BNEPLL3
 5800.DORND LDARAND+1:TAX:ADCRAND+3:STARAND+1:STXRAND+3:LDARAND:TAX:ADCRAND+2:STARAND:STXRAND+2:RTS
 5810.RAND EQUD &34785349
 5815.SQUA2 BPLSQUA:EOR#FF:CLC:ADC#1
 5820.SQUA STAQ:STAP:LDA#0:LDY#8:LSRP:.SQL1 BCCSQ1:CLC:ADCQ:.SQ1 RORA:RORP:DEY:BNESQL1:RTS
 5850.PIX TAY:EOR#128:LSRA:LSRA:LSRA:ORA#&60:STAZP+1:TXA:EOR#128:AND#&F8:STAZP:TYA:AND#7:TAY:TXA:AND#7:TAX
 5860LDATWOS,X:STA(ZP),Y:RTS
 5870.TWOS EQUD &10204080:EQUD&01020408
 5880.CNT EQUW&300:.CNT2 EQUW &1DD:.CNT3 EQUW &333
 5890.ROOT LDYZP+1:LDAZP:STAQ:LDX#0:STXZP:LDA#8:STAP:.LL6 CPXZP:BCCLL7:BNELL8:CPY#&40:BCCLL7:.LL8 TYA:SBC#&40:TAY:TXA:SBCZP:TAX:.LL7 ROLZP:ASLQ:TYA:ROLA:TAY:TXA:ROLA:TAX:ASLQ:TYA:ROLA:TAY:TXA:ROLA:TAX:DECP:BNELL6:RTS
 5900.OSB LDY#0:JMPOSBYTE
 6010.MESS2 EQUS("L.ELTA"):EQUB13
 6020.MESS3 EQUS("L.ELTB"):EQUB13
 6030.MESS4 EQUS("L.ELTC"):EQUB13
 6040.MESS5 EQUS("L.ELTD"):EQUB13
 6050.MESS6 EQUS("L.ELTE"):EQUB13
 6060.MESS7 EQUS("L.ELTF"):EQUB13
 6070.MESS8 EQUS("L.ELTG"):EQUB13
 6100.MESS9 EQUS("L.SHIPS "+STR$~(D%+L%-C%)):EQUB13
 6110.MESS10 EQUS("L.WORDS9"):EQUB13
 6120.MESS11 EQUS("L.DIALSHP"):EQUB13
 7000]
 7020NEXTZ
 7100PRINT~CODE O% P% ENTRY
 7110INPUT"Insert assembly disk and hit RETURN"A$
 7120OSCLI("S.MCTEST "+STR$~CODE +" "+STR$~O% +" "+STR$~ENTRY +" 900")
 7200END
 8000DATA67
 8100DATA 22,4,28,2,17,15,16
 8110DATA 23,0, 6,31,0,0,0,0,0,0
 8112DATA 23,0,12,12,0,0,0,0,0,0
 8114DATA 23,0,13, 0,0,0,0,0,0,0
 8116DATA 23,0, 1,32,0,0,0,0,0,0
 8118DATA 23,0, 2,45,0,0,0,0,0,0
 8120DATA 23,0,10,32,0,0,0,0,0,0
 9100DATA1,1,0,111,-8,4,1,8, 8,-2,0,-1,126,44
 9110DATA2,1,14,-18,-1,44,32,50, 6,1,0,-2,120,126
 9120DATA3,1,1,-1,-3,17,32,128,1,0,0,-1,1,1
 9130DATA4,1,4,-8,44,4,6,8,22,0,0,-127,126,0
10000DEFPROCP(A):?O%=A:O%=O%+1:P%=P%+1:ENDPROC
10100DEF FNE(I%)
10110[OPTZ
10120LDX#((E%+I%*14)MOD256):LDY#((E%+I%*14)DIV256):LDA#8:JSROSWORD
10130]
10140=""
